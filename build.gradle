buildscript {
    ext.pythonPluginVersion = pythonPluginVersion
    ext.pycharmSandbox = project.buildDir.absolutePath + File.separator + "pycharm-sandbox"
    ext.studioSandbox = project.buildDir.absolutePath + File.separator + "studio-sandbox"
    ext.kotlinVersion = "1.2.21"
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.2.17" apply false
    id "java"
    id "idea"
    id "de.undercouch.download" version "3.2.0"
}


group = 'org.jetbrains.edu'
def buildNumber = System.getenv("BUILD_NUMBER")
version = "$pluginVersion-${buildNumber == null ? "SNAPSHOT" : buildNumber}"

import de.undercouch.gradle.tasks.download.Download
import org.apache.tools.ant.taskdefs.condition.Os

task downloadJavaFx(type: Download) {
    overwrite true
    src javafx
    dest "${project.buildDir}/javafx.zip"
}

task prepareJavaFx(type: Copy) {
    def javafxFile = file("${project.buildDir}/javafx.zip")
    onlyIf { javafxFile.exists() }
    from zipTree(javafxFile)
    into file("${project.buildDir}/javafx")
}

prepareJavaFx.dependsOn downloadJavaFx

allprojects {
    apply plugin: "org.jetbrains.intellij"
    apply plugin: "java"
    apply plugin: "kotlin"


    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
    targetCompatibility = '1.8'
    sourceCompatibility = '1.8'

    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/jetbrains/markdown' }
    }

    intellij {
        if (project.hasProperty("androidStudioPath")) {
            localPath androidStudioPath
        } else {
            localPath "${rootProject.projectDir}/dependencies/studio-$studioVersion-$studioBuildVersion/${studioContentPath()}"
        }
    }

    compileKotlin {
        // see https://youtrack.jetbrains.com/issue/KT-19737
        destinationDir = compileJava.destinationDir
        kotlinOptions {
            jvmTarget = "1.8"
            languageVersion = "1.1"
            apiVersion = "1.1"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            languageVersion = "1.1"
            apiVersion = "1.1"
        }
    }

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jre8:1.1.61"
        compile group: 'org.twitter4j', name: 'twitter4j-core', version: '4.0.1'
        compile("org.jetbrains:markdown:${markdownParserVersion}") {
            exclude module: 'kotlin-runtime'
            exclude module: 'kotlin-stdlib'
        }
        compileOnly fileTree(dir: "${rootProject.buildDir}/javafx/jre/lib/ext")
    }
}

intellij {
    if (project.hasProperty("androidStudioPath")) {
        localPath androidStudioPath
    } else {
        if (!rootProject.hasProperty("studioVersion")) throw IllegalStateException("studioVersion is unspecified")
        if (!rootProject.hasProperty("studioBuildVersion")) throw IllegalStateException("studioBuildVersion is unspecified")
        def osFamily = getOsFamily()
        if (osFamily == null) throw IllegalStateException("current os family is unsupported")

        def studioZip = file("${rootProject.projectDir}/dependencies/studio-$studioVersion-$studioBuildVersion-${osFamily}.zip")
        if (!studioZip.exists()) {
            download {
                src "http://dl.google.com/dl/android/studio/ide-zips/$studioVersion/android-studio-ide-$studioBuildVersion-${osFamily}.zip"
                dest studioZip
            }
        }

        def studioFolder = file("${rootProject.projectDir}/dependencies/studio-$studioVersion-$studioBuildVersion")
        if (!studioFolder.exists()) {
            copy {
                from zipTree(studioZip)
                into studioFolder
            }
        }

        localPath "$studioFolder/${studioContentPath()}"
    }

    if (project.hasProperty("customSinceBuild")) {
        patchPluginXml.sinceBuild = customSinceBuild
    }
    patchPluginXml.changeNotes file("changes.html").getText()
    patchPluginXml.pluginDescription file("description.html").getText()
    pluginName 'EduTools'
    updateSinceUntilBuild true
    downloadSources false
    plugins 'junit', 'Kotlin', "PythonCore:$pythonPluginVersion"
}

static String getOsFamily() {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) return "windows"
    if (Os.isFamily(Os.FAMILY_MAC)) return "mac"
    if (Os.isFamily(Os.FAMILY_UNIX) && !Os.isFamily(Os.FAMILY_MAC)) return "linux"
    return null
}

static String studioContentPath() {
    def osFamily = getOsFamily()
    if (osFamily == null) throw IllegalStateException("current os family is unsupported")
    if (osFamily == "mac") {
        return "Android Studio.app/Contents"
    } else {
        return "android-studio"
    }
}

task configurePyCharm {
    doLast {
        if (!project.hasProperty("pycharmPath")) {
            throw new InvalidUserDataException("Path to PyCharm installed locally is needed\nDefine \"pycharmPath\" property")
        }
        intellij.sandboxDirectory pycharmSandbox
        intellij.alternativeIdePath pycharmPath
    }
}

task configureAndroidStudio {
    doLast {
        if (!project.hasProperty("androidStudioPath")) {
            throw new InvalidUserDataException("Path to Android Studio installed locally is needed\nDefine \"androidStudioPath\" property")
        }
        intellij.sandboxDirectory studioSandbox
        intellij.alternativeIdePath androidStudioPath
    }
}

task copyXmls(type: Copy) {
    def resultingMetaInf = "${sourceSets.main.output.resourcesDir}/META-INF"

    for (def subProject : project.subprojects) {
        from "${subProject.name}/resources/META-INF"
        into resultingMetaInf
        include "*.xml"
    }
}

jar.dependsOn(copyXmls)

task removePyCharm(type: Delete) {
    doLast {
        file(pycharmSandbox + File.separator + "plugins" + File.separator + "python-ce").deleteDir()
    }
}

// we need this so as not to install python plugin on PyCharm
prepareSandbox.finalizedBy(removePyCharm)

subprojects {
    sourceSets {
        main {
            java.srcDirs 'src'
            resources.srcDirs 'resources'
            kotlin.srcDirs 'src'
        }

        test {
            java.srcDirs 'testSrc'
            resources.srcDirs 'testResources'
            kotlin.srcDirs 'testSrc'
        }
    }

    project.tasks.getByPath("runIde").enabled false
    project.tasks.getByPath("prepareSandbox").enabled false
}

sourceSets {
    main {
        resources.srcDirs 'resources'
    }
}

configure(project(':educational-core')) {

    task downloadColorFile(type: Download) {
        overwrite false
        src 'https://raw.githubusercontent.com/ozh/github-colors/master/colors.json'
        dest "${projectDir}/resources/languageColors/colors.json"
    }
}

configure([project(':Edu-Python'), project(':Edu-Java'), project(':Edu-Kotlin')]) {
    dependencies {
        compile project(':educational-core')
        testCompile project(':educational-core').sourceSets.test.output
    }
}

configure([project(':educational-core'), project(':Edu-Java')]) {
    intellij.plugins 'junit', 'properties', 'gradle', 'Groovy', 'smali'
}

configure(project(':Edu-Kotlin')) {
    intellij.plugins 'junit', 'Kotlin', 'properties', 'gradle', 'Groovy', 'smali'


    task createTwitterProperties() {
        def twitterFile = file("${projectDir}/resources/twitter/kotlin_koans/oauth_twitter.properties")
        Properties props = new Properties()
        props.setProperty("consumerKey", consumerKey)
        props.setProperty("consumerSecret", consumerSecret)
        props.store(twitterFile.newWriter(), null)
    }
    processResources.dependsOn(createTwitterProperties)
}

configure(project(':Edu-Python')) {
    intellij.plugins "PythonCore:$pythonPluginVersion"
}

runIde.systemProperty("-Didea.is.internal", "true")
runIde.systemProperty("-ea", "")

dependencies {
    compile project(':educational-core'), project(':Edu-Python'), project(':Edu-Kotlin'), project(':Edu-Java')
}

idea {
    project {
        jdkName = 1.8
        languageLevel = 1.8
        vcs = 'Git'
    }
}

allprojects {
    test {
        if (rootProject.hasProperty("stepik_test_client_secret")) {
            environment 'STEPIK_TEST_CLIENT_SECRET', stepik_test_client_secret
        }
    }
}