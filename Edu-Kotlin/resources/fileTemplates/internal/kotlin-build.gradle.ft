buildscript {
    ext.kotlin_version = '1.2.0'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

class MyListener implements TestListener {

    @Override
    void beforeSuite(TestDescriptor suite) {

    }

    @Override
    void afterSuite(TestDescriptor suite, TestResult result) {
    }

    @Override
    void beforeTest(TestDescriptor testDescriptor) {

    }

    public void afterTest(TestDescriptor test, TestResult result) {
        if (result.resultType == TestResult.ResultType.FAILURE) {
            println("#educational_plugin FAILED + " + (result.exception.message ?: "Wrong answer"));
        }
    }
}

def printOutput(def output) {
    return tasks.create("printOutput") {
        for (line in output.toString().split(System.getProperty("line.separator"))) {
            println "#educational_plugin" + line
        }
    }
}

subprojects {
    apply plugin: 'application'
    apply plugin: 'java'
    apply plugin: 'kotlin'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    compileKotlin.destinationDir = compileJava.destinationDir

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    sourceSets {
        main {
            kotlin.srcDir 'src'
            java.srcDir 'src'
        }
        test {
            kotlin.srcDir 'test'
            java.srcDir 'test'
        }
    }

    mainClassName = project.hasProperty("mainClass") ? project.getProperty("mainClass") : ""

    test.outputs.upToDateWhen { false }

    gradle.addListener(new MyListener())

    def runOutput = new ByteArrayOutputStream()
    tasks.run.setStandardOutput(runOutput)
    tasks.run.doLast { printOutput(runOutput) }
}

project(':util') {
    dependencies {
        compile group: 'junit', name: 'junit', version: '4.12'
    }
}

configure(subprojects.findAll {it.name != 'util'}) {
  dependencies {
    compile project(':util').sourceSets.main.output
    testCompile project(':util').sourceSets.test.output
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '$GRADLE_VERSION$'
}
